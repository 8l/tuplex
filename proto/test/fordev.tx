## step-by-step support for ranges, arrays, sequences, and for-loops 

## var-arg functions:
var_func( x : UInt, vars : Float... ) -> UInt {
    return vars.L;  ## number of var-args passed
}


## Array literals:
## array_literal :=    ‘[‘ expr_list ‘]’
## Future: expansion operator '...'

foofunc( array : &[]UByte ) { }

foofunc( array : &[]Int ) { }

foofunc( array : &[]Float ) { }


array_construction( i : UByte ) {
## Construction via array literal syntax:
    a0 := [ i, i, i ];
    a1 := [ 1, 2, 3 ];
    a2 := [ Int(1), 2, 3 ];
    #experr 1: ae := [];  ## invalid, no element type info

    a3 := [3]Int( 1, 2, 3 );
    a4 := []Int( 1, 2, 3 );
    a5 := []Int();  ## empty array of Ints
    #experr 1: ae := [2]Int( 1, 2, 3 );
    #experr 1: ae := [4]Int( 1, 2, 3 );
    #experr 1: ae := [0]Int( 1, 2, 3 );
    #experr 2: ae := [-1]Int( 1, 2, 3 );
    #experr 2: ae := [2.5]Int( 1, 2, 3 );
    #experr 2: ae := [i]Int( 1, 2, 3 );

    foofunc( a0 );
    foofunc( a1 );
    foofunc( a2 );
    foofunc( a3 );
    foofunc( a4 );
    foofunc( a5 );


## Construction via Array type's constructor:
    a10 := <Array<UByte,3>>( [ 1, 2, 3 ] );
    a11 := <Array<Int,3>>( [ 1I, 2, 3 ] );
    a12 := <Array<Int,0>>( []Int() );
    #experr 1: ae := <Array<Int,3>>( [ 1, 2 ] );
    #experr 1: ae := <Array<Int,3>>( [ 1, 2, 3, 4 ] );
    #experr 1: ae := <Array<Int,3>>( [ 1.1, 2.2, 3.3 ] );

    type MyFA Array<Float,3>;
    aa := MyFA( [ 1.1, 2.2, 3.3 ] );
    #experr 1: ae := MyFA( [ 1.1, 2.2 ] );
    #experr 1: ae := MyFA( [ 1.1, 2.2, 3.3, 4.4 ] );
    #experr 1: ae := MyFA( [ 1, 2, 3 ] );

    ## syntactic sugar that is currently not supported:
    #experr 1: ae := <Array<Int,0>>();
    #experr 1: ae := <Array<Int,3>>( 1, 2, 3 );

    ## not supported to automatically infer generic type parameters:
    #experr 1: aa := <Array<Int>>( [ 1, 2, 3 ] );
    #experr 1: aa := <Array<>>( [ 1, 2, 3 ] );
    #experr 1: aa := <Array>( [ 1, 2, 3 ] );

/*
    aa := new [3]Int( 1, 2, 3 );
    aa := new []Int( 1, 2, 3 );
    aa := new []Int();
    aa := new Array<Int,3>( [ 1, 2, 3 ] );
    aa := new Array<Int>( [ 1, 2, 3 ] );
    aa := new Array<Int>();
*/

##  FUTURE: In-place expansion of sequences:
##  aa := Array<Int>( 1, 2, 3, a1... );
}



/*
for_loops() {
    for i in 0:10 {
    }

    for i in 0:9,
        j in 9:-1:0 {
    }

    for i, iter in str[0:9] {
        print(i)
        if i == 'a'
            iter.next()
    }
}
*/

main() -> Int {
    n := var_func( 0, 1.1, 2.2, 3.3 );
    n := var_func( 0, [ 1.1, 2.2, 3.3 ] );
    array_construction( 1 );
    return Int( n );
}
